#version 430

// Note Anvil define system assumes first line is alway a #version so don't rearrange

//
// Copyright (c) 2016 Advanced Micro Devices, Inc. All rights reserved.
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

layout( local_size_x = 64, local_size_y = 1, local_size_z = 1 ) in;

struct bbox
{
    vec4 pmin;
    vec4 pmax;
};

#define BvhNode bbox

struct ray
{
    vec4 o;
    vec4 d;
    ivec2 extra;
    ivec2 padding;
};

struct ShapeData
{
    int id;
    int bvhidx;
    int mask;
    int padding1;
    vec4 m0;
    vec4 m1;
    vec4 m2;
    vec4 m3;
    vec4  linearvelocity;
    vec4  angularvelocity;
};

struct Face
{
    // Vertex indices
    int idx0;
    int idx1;
    int idx2;
    int shapeidx;
    // Primitive ID
    int id;
    // Idx count
    int cnt;

    ivec2 padding;
};

struct Intersection
{
    int shapeid;
    int primid;
    ivec2 padding;
    vec4 uvwt;
};

layout( std140, binding = 0 ) buffer restrict readonly NodesBlock
{
    bbox Nodes[];
};

layout( std140, binding = 1 ) buffer restrict readonly VerticesBlock
{
    vec4 Vertices[];
};

layout( std140, binding = 2 ) buffer restrict readonly FacesBlock
{
    Face Faces[];
};

layout( std140, binding = 3 ) buffer restrict readonly ShapesBlock
{
    ShapeData Shapes[];
};

layout( std140, binding = 4 ) buffer restrict readonly RootidxBlock
{
    int Rootidx;
};

layout( std140, binding = 5 ) buffer restrict readonly RaysBlock
{
    ray Rays[];
};

layout( std140, binding = 6 ) buffer restrict readonly OffsetBlock
{
    int Offset;
};

layout( std140, binding = 7 ) buffer restrict readonly NumraysBlock
{
    int Numrays;
};

layout( std430, binding = 8 ) buffer restrict writeonly HitsBlock
{
    Intersection Hits[];
};
layout( std430, binding = 8 ) buffer restrict writeonly HitsResultBlock
{
    int Hitresults[];
};

#define PI 3.14159265358979323846f

bool Ray_IsActive( in ray r )
{
    return 0 != r.extra.y ;
}

int Ray_GetMask( in ray r )
{
    return r.extra.x;
}

bool IntersectSceneAny2L( in ray r );
void IntersectSceneClosest2L( in ray r, inout Intersection isect);

void IntersectAny2L()
{
    uint globalID = gl_GlobalInvocationID.x;

    // Handle only working subset
    if (globalID < Numrays)
    {
        // Fetch ray
        int idx = Offset + int(globalID);
        ray r = Rays[idx];

        if (Ray_IsActive(r))
        {
            // Calculate any intersection
            Hitresults[idx] = IntersectSceneAny2L( r ) ? 1 : -1;
        }
    }
}

void IntersectAnyRC2L()
{
    IntersectAny2L();
}

void IntersectClosest2L()
{
    uint globalID = gl_GlobalInvocationID.x;

    // Handle only working subset
    if (globalID < Numrays)
    {
        // Fetch ray
        int idx = Offset + int(globalID);
        ray r = Rays[idx];

        if (Ray_IsActive( r ))
        {
            // Calculate closest hit
            Intersection isect;

            IntersectSceneClosest2L( r, isect);

            // Write data back in case of a hit
            Hits[idx] = isect;
        }
    }
}

void IntersectClosestRC2L()
{
    IntersectClosest2L();
}


#define STARTIDX(x)     ((int(x.pmin.w)))
#define SHAPEIDX(x)     ((int(x.pmin.w)))
#define LEAFNODE(x)     ((x.pmin.w) != -1.f)

vec3 transform_point(in vec3 p, in vec4 m0, in vec4 m1, in vec4 m2, in vec4 m3)
{
    vec3 res;
    res.x = m0.x * p.x + m0.y * p.y + m0.z * p.z + m0.w;
    res.y = m1.x * p.x + m1.y * p.y + m1.z * p.z + m1.w;
    res.z = m2.x * p.x + m2.y * p.y + m2.z * p.z + m2.w;
    return res;
}

vec3 transform_vector(in vec3 p, in vec4 m0, in vec4 m1, in vec4 m2, in vec4 m3)
{
    vec3 res;
    res.x = m0.x * p.x + m0.y * p.y + m0.z * p.z;
    res.y = m1.x * p.x + m1.y * p.y + m1.z * p.z;
    res.z = m2.x * p.x + m2.y * p.y + m2.z * p.z;
    return res;
}


ray transform_ray( in ray r, in vec4 m0, in vec4 m1, in vec4 m2, in vec4 m3)
{
    ray res;
    res.o.xyz = transform_point(r.o.xyz, m0, m1, m2, m3);
    res.d.xyz = transform_vector(r.d.xyz, m0, m1, m2, m3);
    res.o.w = r.o.w;
    res.d.w = r.d.w;
    return res;
}

bool IntersectBox(in ray r, in vec3 invdir, in bbox box, in float maxt)
{
    const vec3 f = (box.pmax.xyz - r.o.xyz) * invdir;
    const vec3 n = (box.pmin.xyz - r.o.xyz) * invdir;
          
    const vec3 tmax = max(f, n);
    const vec3 tmin = min(f, n);

    const float t1 = min(min(tmax.x, min(tmax.y, tmax.z)), maxt);
    const float t0 = max(max(tmin.x, max(tmin.y, tmin.z)), 0.f);

    return (t1 >= t0) ? true : false;
}

bool IntersectTriangleP( in ray r, in vec3 v1, in vec3 v2, in vec3 v3 )
{
    const vec3 e1 = v2 - v1;
    const vec3 e2 = v3 - v1;
    const vec3 s1 = cross(r.d.xyz, e2);
    const float  invd = 1.0f/(dot(s1, e1));
    const vec3 d = r.o.xyz - v1;
    const float  b1 = dot(d, s1) * invd;
    const vec3 s2 = cross(d, e1);
    const float  b2 = dot(r.d.xyz, s2) * invd;
    const float temp = dot(e2, s2) * invd;
    
    if ( b1 < 0.f || b1 > 1.f || b2 < 0.f || b1 + b2 > 1.f || temp < 0.f || temp > r.o.w )
    {
        return false;
    }

    else
    {
        return true;
    }
}

bool IntersectTriangle( in ray r, in vec3 v1, in vec3 v2, in vec3 v3, inout Intersection isect)
{
    const vec3 e1 = v2 - v1;
    const vec3 e2 = v3 - v1;
    const vec3 s1 = cross(r.d.xyz, e2);
    const float  invd = 1.0f/(dot(s1, e1));
    const vec3 d = r.o.xyz - v1;
    const float  b1 = dot(d, s1) * invd;
    const vec3 s2 = cross(d, e1);
    const float  b2 = dot(r.d.xyz, s2) * invd;
    const float temp = dot(e2, s2) * invd;

    if (b1 < 0.f || b1 > 1.f || b2 < 0.f || b1 + b2 > 1.f || temp < 0.f || temp > isect.uvwt.w)
    {
        return false;
    }
    else
    {
        isect.uvwt = vec4(b1, b2, 0.f, temp);
        return true;
    }
}


//  intersect a ray with leaf BVH node
bool IntersectLeafAny( in BvhNode node, in ray r )
{
    vec3 v1, v2, v3;
    Face face;

    int start = STARTIDX(node);
    face = Faces[start];
    v1 = Vertices[face.idx0].xyz;
    v2 = Vertices[face.idx1].xyz;
    v3 = Vertices[face.idx2].xyz;

    if (IntersectTriangleP(r, v1, v2, v3))
    {
        return true;
    }

    return false;
}

//  intersect a ray with leaf BVH node
void IntersectLeafClosest( in BvhNode node, in ray r, in int shapeid, inout Intersection isect )
{
    vec3 v1, v2, v3;
    Face face;

    int start = STARTIDX(node);
    face = Faces[start];
    v1 = Vertices[face.idx0].xyz;
    v2 = Vertices[face.idx1].xyz;
    v3 = Vertices[face.idx2].xyz;

    if (IntersectTriangle(r, v1, v2, v3, isect))
    {
        isect.primid = face.id;
        isect.shapeid = shapeid;
    }
}

// intersect Ray against the whole BVH2L structure
bool IntersectSceneAny2L(in ray r)
{
    // Precompute invdir for bbox testing
    vec3 invdir = vec3(1.f, 1.f, 1.f) / r.d.xyz;
    vec3 invdirtop = vec3(1.f, 1.f, 1.f) / r.d.xyz;
    // We need to keep original ray around for returns from bottom hierarchy
    ray topray = r;

    // Fetch top level BVH index
    int idx = Rootidx;
    // -1 indicates we are traversing top level
    int topidx = -1;
    while (idx != -1)
    {
        // Try intersecting against current node's bounding box.
        BvhNode node = Nodes[idx];
        if (IntersectBox(r, invdir, node, r.o.w))
        {
            if (LEAFNODE(node))
            {
                // If this is the leaf it can be either a leaf containing primitives (bottom hierarchy)
                // or containing another BVH (top level hierarhcy)
                if (topidx != -1)
                {
                    // This is bottom level, so intersect with a primitives
                    IntersectLeafAny(node, r);

                    // And goto next node
                    idx = int(node.pmax.w);
                }
                else
                {
                    // This is top level hierarchy leaf
                    // Save top node index for return
                    topidx = idx;
                    // Get shape descrition struct index
                    int shapeidx = SHAPEIDX(node);
                    // Get shape mask
                    int shapemask = Shapes[shapeidx].mask;
                    // Drill into 2nd level BVH only if the geometry is not masked vs current ray
                    // otherwise skip the subtree
                    if ( ( Ray_GetMask(r) & shapemask) != 0 )
                    {
                        // Fetch bottom level BVH index
                        idx = Shapes[shapeidx].bvhidx;

                        // Fetch BVH transform
                        vec4 wmi0 = Shapes[shapeidx].m0;
                        vec4 wmi1 = Shapes[shapeidx].m1;
                        vec4 wmi2 = Shapes[shapeidx].m2;
                        vec4 wmi3 = Shapes[shapeidx].m3;

                        // Apply linear motion blur (world coordinates)
                        //vec4 lmv = Shapes[shapeidx].linearvelocity;
                        //vec4 amv = Shapes[SHAPEDATAIDX(node)].angularvelocity;
                        //r.o.xyz -= (lmv.xyz*r.d.w);
                        // Transfrom the ray
                        r = transform_ray(r, wmi0, wmi1, wmi2, wmi3);
                        //rotate_ray(r, amv);
                        // Recalc invdir
                        invdir = vec3(1.f, 1.f, 1.f) / r.d.xyz;
                        // And continue traversal of the bottom level BVH
                        continue;
                    }
                    else
                    {
                        // Skip the subtree
                        idx = -1;
                    }
                }
            }
            // Traverse child nodes otherwise.
            else
            {
                // This is an internal node, proceed to left child (it is at current + 1 index)
                idx = idx + 1;
            }
        }
        else
        {
            // We missed the node, goto next one
            idx = int(node.pmax.w);
        }

        // Here check if we ended up traversing bottom level BVH
        // in this case idx = 0xFFFFFFFF and topidx has valid value
        if (idx == -1 && topidx != -1)
        {
            //  Proceed to next top level node
            idx = int(Nodes[topidx].pmax.w);
            // Set topidx
            topidx = -1;
            // Restore ray here
            r = topray;
            // Restore invdir
            invdir = invdirtop;
        }
    }

    return false;
}

// intersect Ray against the whole BVH2L structure
void IntersectSceneClosest2L(in ray r, inout Intersection isect)
{
    // Init intersection
    isect.uvwt = vec4(0.f, 0.f, 0.f, r.o.w);
    isect.shapeid = -1;
    isect.primid = -1;

    isect.padding.x = 667;
    isect.padding.y = r.padding.x;

    // Precompute invdir for bbox testing
    vec3 invdir = vec3(1.f, 1.f, 1.f) / r.d.xyz;
    vec3 invdirtop = vec3(1.f, 1.f, 1.f) / r.d.xyz;
    // We need to keep original ray around for returns from bottom hierarchy
    ray topray = r;

    // Fetch top level BVH index
    int idx = Rootidx;
    // -1 indicates we are traversing top level
    int topidx = -1;
    // Current shape id
    int shapeid = -1;
    while (idx != -1)
    {
        // Try intersecting against current node's bounding box.
        BvhNode node = Nodes[idx];
        if (IntersectBox(r, invdir, node, isect.uvwt.w))
        {
            if (LEAFNODE(node))
            {
                // If this is the leaf it can be either a leaf containing primitives (bottom hierarchy)
                // or containing another BVH (top level hierarhcy)
                if (topidx != -1)
                {
                    // This is bottom level, so intersect with a primitives
                    IntersectLeafClosest(  node, r, shapeid, isect );

                    // And goto next node
                    idx = int(node.pmax.w);
                }
                else
                {
                    // This is top level hierarchy leaf
                    // Save top node index for return
                    topidx = idx;
                    // Get shape descrition struct index
                    int shapeidx = SHAPEIDX(node);
                    // Get shape mask
                    int shapemask = Shapes[shapeidx].mask;
                    // Drill into 2nd level BVH only if the geometry is not masked vs current ray
                    // otherwise skip the subtree
                    if ( ( Ray_GetMask(r) & shapemask ) != 0 )
                    {
                        // Fetch bottom level BVH index
                        idx = Shapes[shapeidx].bvhidx;
                        shapeid = Shapes[shapeidx].id;

                        // Fetch BVH transform
                        vec4 wmi0 = Shapes[shapeidx].m0;
                        vec4 wmi1 = Shapes[shapeidx].m1;
                        vec4 wmi2 = Shapes[shapeidx].m2;
                        vec4 wmi3 = Shapes[shapeidx].m3;

                        // Apply linear motion blur (world coordinates)
                        //vec4 lmv = Shapes[shapeidx].linearvelocity;
                        //vec4 amv = Shapes[SHAPEDATAIDX(node)].angularvelocity;
                        //r.o.xyz -= (lmv.xyz*r.d.w);
                        // Transfrom the ray
                        r = transform_ray(r, wmi0, wmi1, wmi2, wmi3);
                        // rotate_ray(r, amv);
                        // Recalc invdir
                        invdir = vec3(1.f, 1.f, 1.f) / r.d.xyz;
                        // And continue traversal of the bottom level BVH
                        continue;
                    }
                    else
                    {
                        idx = -1;
                    }
                }
            }
            // Traverse child nodes otherwise.
            else
            {
                // This is an internal node, proceed to left child (it is at current + 1 index)
                idx = idx + 1;
            }
        }
        else
        {
            // We missed the node, goto next one
            idx = int(node.pmax.w);
        }

        // Here check if we ended up traversing bottom level BVH
        // in this case idx = -1 and topidx has valid value
        if (idx == -1 && topidx != -1)
        {
            //  Proceed to next top level node
            idx = int(Nodes[topidx].pmax.w);
            // Set topidx
            topidx = -1;
            // Restore ray here
            r.o = topray.o;
            r.d = topray.d;
            // Restore invdir
            invdir = invdirtop;
        }
    }
}
